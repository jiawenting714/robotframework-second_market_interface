*** Settings ***
Suite Setup       setup
Test Setup        setup
Library           RequestsLibrary
Library           String
Resource          公共资源.txt
Library           MyMongodbLibrary

*** Variables ***
${app_search_url}    /api/remarket/customer_query?filter_name=app-customer&limit=10    # 手机app查询记录管理：查询接口
${create_target_url}    /api/remarket/target
${filter_rule}    %7B%22app_version%22%3A%5B%5D%2C%22view_module%22%3A%5B%5D%2C%22login_times%22%3A%5B%7B%22ge%22%3A1%2C%22le%22%3A100000000%7D%5D%2C%22last_use_time%22%3A%7B%22start%22%3A%222015-01-01%22%2C%22end%22%3A%222016-12-31%22%7D%2C%22customer%22%3A%7B%22province%22%3A%5B%5D%2C%22city%22%3A%5B%5D%7D%2C%22if_phone%22%3A0%2C%22if_wechat%22%3A0%2C%22if_gfportal%22%3A0%7D    # 手机app新建查询条件
${query_bigdata}    /api/remarket/bigdata/query_aggregate    # 手机app-大数据接口
${create_customer_query_url}    /api/remarket/customer_query    # 保存新建查询记录url

*** Test Cases ***
查询手机APP记录_01_无权限用户登录查询
    &{user_info}    create dictionary    username=${nopermission_userinfo['username']}    password=${nopermission_userinfo['password']}
    ${session}    登录    ${login_interface}    ${user_info['username']}    ${user_info['password']}
    ${response}    get request    global_session    ${app_search_url}    headers=&{header}
    log    请求响应时间: ${response.elapsed.total_seconds()}s
    log    ${response.text}
    should be equal as integers    ${response.json()['code']}    ${0}
    #目前增加无权限用户可查看数据
    #should be equal as strings    ${response.json()['msg']}    您没有权限访问

查询手机APP记录_02_用户未登录查询
    create global session    global_session    ${base_url}
    ${response}    get request    global_session    ${app_search_url}    headers=&{header}
    log    请求响应时间: ${response.elapsed.total_seconds()}s
    log    ${response.text}
    should be equal as integers    ${response.json()['code']}    ${17}
    should be equal as strings    ${response.json()['msg']}    认证失败:Error: 未登录

查询手机APP记录_03_有权限用户登录查询
    ${response}    get request    global_session    ${app_search_url}    headers=&{header}
    log    请求响应时间: ${response.elapsed.total_seconds()}s
    log    ${response.text}
    should be equal as integers    ${response.json()['code']}    ${0}

创建营销对象_04_临时对象
    #拼接json_data条件
    ${query_id}    获取手机app查询记录id
    ${target_name}    生成对象名称
    ${json_data}    create dictionary    name=${target_name}    frequency=once    queryId=${query_id}
    ${response}    post request    global_session    ${create_target_url}    data=${json_data}    headers=&{header}
    log    请求响应时间: ${response.elapsed.total_seconds()}s
    log    ${response.text}
    should be equal as strings    ${response.json()['data']}    ok
    should be equal as integers    ${response.json()['code']}    ${0}
    [Teardown]    删除营销对象    ${test_target_name}

创建营销对象_05_临时对象_营销对象名称为空
    #拼接json_data条件
    ${query_id}    获取手机app查询记录id
    ${target_name}    生成对象名称
    ${json_data}    create dictionary    name=${EMPTY}    frequency=once    queryId=${query_id}
    ${response}    post request    global_session    ${create_target_url}    data=${json_data}
    log    请求响应时间: ${response.elapsed.total_seconds()}s
    log    ${response.text}
    should be equal as strings    ${response.json()['msg']}    ValidationError: child "name" fails because ["name" is required]
    should be equal as integers    ${response.json()['code']}    ${12}

创建营销对象_06_定时对象_frequency为空
    #拼接json_data条件
    ${query_id}    获取手机app查询记录id
    ${target_name}    生成对象名称
    ${endtime}    get current date    increment=${3600*24*2}    result_format=%Y-%m-%d %H:%M:%S
    ${json_data}    create dictionary    frequency=${EMPTY}    name=${target_name}    queryId=${query_id}    period_time=09:00    endTime=${endtime}
    ${response}    post request    global_session    ${create_target_url}    data=${json_data}    headers=&{header}
    log    请求响应时间: ${response.elapsed.total_seconds()}s
    log    ${response.text}
    should be equal as strings    ${response.json()['msg']}    ValidationError: child "frequency" fails because ["frequency" is not allowed to be empty]
    should be equal as integers    ${response.json()['code']}    ${12}

创建营销对象_07_定时对象_每日
    #拼接json_data条件
    ${query_id}    获取手机app查询记录id
    ${target_name}    生成对象名称
    ${endtime}    get current date    increment=${3600*24*2}    result_format=%Y-%m-%d %H:%M:%S
    ${query_id}    convert to string    ${query_id}
    ${json_data}    create dictionary    name=${target_name}    frequency=daily    queryId=${query_id}    period_time=09:00    endTime=${endtime}
    ${response}    post request    global_session    ${create_target_url}    data=${json_data}    headers=&{header}
    log    请求响应时间: ${response.elapsed.total_seconds()}s
    log    ${response.text}
    should be equal as strings    ${response.json()['data']}    ok
    should be equal as integers    ${response.json()['code']}    ${0}
    [Teardown]    删除营销对象    ${test_target_name}

创建营销对象_08_定时对象_每日_营销对象名称为空
    #拼接json_data条件
    ${query_id}    获取手机app查询记录id
    ${target_name}    生成对象名称
    ${endtime}    get current date    increment=${3600*24*2}    result_format=%Y-%m-%d %H:%M:%S
    ${json_data}    create dictionary    frequency=${EMPTY}    name=${EMPTY}    queryId=${query_id}    period_time=09:00    endTime=${endtime}
    ${response}    post request    global_session    ${create_target_url}    data=${json_data}    headers=&{header}
    log    请求响应时间: ${response.elapsed.total_seconds()}s
    log    ${response.text}
    should be equal as strings    ${response.json()['msg']}    ValidationError: child "name" fails because ["name" is not allowed to be empty]
    should be equal as integers    ${response.json()['code']}    ${12}

创建营销对象_09_定时对象_每周
    #拼接json_data条件
    ${query_id}    获取手机app查询记录id
    ${target_name}    生成对象名称
    ${endtime}    get current date    increment=${3600*24*2}    result_format=%Y-%m-%d %H:%M:%S
    #每周二
    ${json_data}    create dictionary    name=${target_name}    frequency=weekly    queryId=${query_id}    period_time=09:00    period_date=2
    ...    endTime=${endtime}
    ${response}    post request    global_session    ${create_target_url}    data=${json_data}    headers=&{header}
    log    请求响应时间: ${response.elapsed.total_seconds()}s
    log    ${response.text}
    should be equal as strings    ${response.json()['data']}    ok
    should be equal as integers    ${response.json()['code']}    ${0}
    [Teardown]    删除营销对象    ${test_target_name}

创建营销对象_10_定时对象_每周_period_date大于6
    #拼接json_data条件
    ${query_id}    获取手机app查询记录id
    ${target_name}    生成对象名称
    ${endtime}    get current date    increment=${3600*24*2}    result_format=%Y-%m-%d %H:%M:%S
    #
    ${json_data}    create dictionary    name=${target_name}    frequency=weekly    queryId=${query_id}    period_time=09:00    period_date=8
    ...    endTime=${endtime}
    ${response}    post request    global_session    ${create_target_url}    data=${json_data}    headers=&{header}
    log    请求响应时间: ${response.elapsed.total_seconds()}s
    log    ${response.text}
    #异常检查
    should be equal as strings    ${response.json()['data']}    ok
    should be equal as integers    ${response.json()['code']}    ${0}
    [Teardown]    删除营销对象    ${test_target_name}

创建营销对象_11_定时对象_每周_营销对象名称为空
    #拼接json_data条件
    ${query_id}    获取手机app查询记录id
    ${target_name}    生成对象名称
    ${endtime}    get current date    increment=${3600*24*2}    result_format=%Y-%m-%d %H:%M:%S
    #每周二
    ${json_data}    create dictionary    name=${EMPTY}    frequency=weekly    queryId=${query_id}    period_time=09:00    period_date=2
    ...    endTime=${endtime}
    ${response}    post request    global_session    ${create_target_url}    data=${json_data}    headers=&{header}
    log    请求响应时间: ${response.elapsed.total_seconds()}s
    log    ${response.text}
    should be equal as strings    ${response.json()['msg']}    ValidationError: child "name" fails because ["name" is not allowed to be empty]
    should be equal as integers    ${response.json()['code']}    ${12}

创建营销对象_12_定时对象_每月
    #拼接json_data条件
    ${query_id}    获取手机app查询记录id
    ${target_name}    生成对象名称
    ${endtime}    get current date    increment=${3600*24*2}    result_format=%Y-%m-%d %H:%M:%S
    #每月9号
    ${json_data}    create dictionary    name=${target_name}    frequency=monthly    queryId=${query_id}    period_time=09:00    period_date=9
    ...    endTime=${endtime}
    ${response}    post request    global_session    ${create_target_url}    data=${json_data}    headers=&{header}
    log    请求响应时间: ${response.elapsed.total_seconds()}s
    log    ${response.text}
    should be equal as strings    ${response.json()['data']}    ok
    should be equal as integers    ${response.json()['code']}    ${0}
    [Teardown]    删除营销对象    ${test_target_name}

创建营销对象_13_定时对象_每月_period_date大于28
    #拼接json_data条件
    ${query_id}    获取手机app查询记录id
    ${target_name}    生成对象名称
    ${endtime}    get current date    increment=${3600*24*2}    result_format=%Y-%m-%d %H:%M:%S
    #每月33号
    ${json_data}    create dictionary    name=${target_name}    frequency=monthly    queryId=${query_id}    period_time=09:00    period_date=33
    ...    endTime=${endtime}
    ${response}    post request    global_session    ${create_target_url}    data=${json_data}    headers=&{header}
    log    请求响应时间: ${response.elapsed.total_seconds()}s
    log    ${response.text}
    should be equal as strings    ${response.json()['data']}    ok
    should be equal as integers    ${response.json()['code']}    ${0}
    [Teardown]    删除营销对象    ${test_target_name}

创建营销对象_14_定时对象_每月_营销对象名称为空
    #拼接json_data条件
    ${query_id}    获取手机app查询记录id
    ${target_name}    生成对象名称
    ${endtime}    get current date    increment=${3600*24*2}    result_format=%Y-%m-%d %H:%M:%S
    #每月2号
    ${json_data}    create dictionary    name=${EMPTY}    frequency=monthly    queryId=${query_id}    period_time=09:00    period_date=2
    ...    endTime=${endtime}
    ${response}    post request    global_session    ${create_target_url}    data=${json_data}    headers=&{header}
    log    请求响应时间: ${response.elapsed.total_seconds()}s
    log    ${response.text}
    should be equal as strings    ${response.json()['msg']}    ValidationError: child "name" fails because ["name" is not allowed to be empty]
    should be equal as integers    ${response.json()['code']}    ${12}

删除手机app记录_15_定时对象_删除手机app记录
    [Setup]    创建手机app记录
    ${customer_info}    查询手机app记录
    #拼接删除记录url
    ${app_delete_url}    catenate    SEPARATOR=${EMPTY}    /api/remarket/customer_query/    ${customer_info['customer_query_id']}
    ${response}    delete request    global_session    ${app_delete_url}
    log    请求响应时间: ${response.elapsed.total_seconds()}s
    log    ${response.text}
    should be equal as strings    ${response.json()['data']}    ok
    should be equal as integers    ${response.json()['code']}    ${0}

*** Keywords ***
setup
    &{header}    create dictionary    Content-Type=application/json
    set suite variable    &{header}    &{header}
    ${session}    登录    ${login_interface}    ${user_info['username']}    ${user_info['password']}
    检查被测环境是否正常    global_session

查询手机app记录
    connect mongodb    ${mongodb_info['host']}    ${mongodb_info['port']}    ${mongodb_info['database']}    ${mongodb_info['username']}    ${mongodb_info['password']}
    select collection    marketing_customer_query
    ${status}    create dictionary    $ne=${9}
    ${filter}    create dictionary    status=${status}    filter_name=app-customer
    ${results}    find    ${filter}    one_or_multi=one
    ${customer_query_info}    create dictionary    customer_query_id=${results[0]['_id']}    customer_count=${results[0]['customer_count']}
    close connection
    [Return]    ${customer_query_info}

生成对象名称
    ${target_name}    生成随机字符串    app_target_name
    set test variable    ${test_target_name}    ${target_name}
    [Return]    ${target_name}

删除营销对象
    [Arguments]    ${target_name}    # 营销对象名称
    connect mongodb    ${mongodb_info['host']}    ${mongodb_info['port']}    ${mongodb_info['database']}    ${mongodb_info['username']}    ${mongodb_info['password']}
    select collection    marketing_target
    ${status}    create dictionary    $ne=${9}
    ${filter}    create dictionary    name=${target_name}    ref_count=${0}    status=${status}
    ${delete_result}    MyMongodbLibrary.delete    ${filter}    multi
    should be true    ${delete_result[0]}
    close connection

获取手机app查询记录id
    ${customer_info}    查询手机app记录
    ${query_id}    set variable    ${customer_info['customer_query_id']}
    ${query_id}    convert to string    ${query_id}
    [Return]    ${query_id}    # 返回查询的手机app记录id

新建查询-手机app
    #拼接json_data
    ${json_data}    create dictionary    filter_name=app-customer    filter_rule=${filter_rule}
    ${response}    post request    global_session    ${query_bigdata}    data=${json_data}    headers=&{header}
    #获取查询结果总数counts
    log    ${response.json()}
    ${counts}    set variable    ${response.json()['data']['count']}
    [Return]    ${counts}

创建手机app记录
    #拼接json_data
    ${count}    新建查询-手机app
    ${json_data}    create dictionary    filter_name=app-customer    filter_rule=${filter_rule}    total=${count}
    ${response}    post request    global_session    ${create_customer_query_url}    data=${json_data}    headers=&{header}
    #检查点
    log    ${response.text}
    should be equal as integers    ${response.json()['code']}    ${0}
    should be equal as strings    ${response.json()['msg']}    ok
